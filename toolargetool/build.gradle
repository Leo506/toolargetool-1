apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'signing'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFile "proguard-rules.pro"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.24.0'
    androidTestImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    // There are no docs here
    archiveClassifier.set('javadoc')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact(sourcesJar)
                artifact(javadocJar)

                groupId = 'com.gu.android'
                artifactId = 'toolargetool'
                version = '0.3.0'

                pom {
                    name = 'toolargetool'
                    description = 'A tool for debugging TransactionTooLargeException on Android'
                    url = 'https://github.com/guardian/toolargetool'
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/guardian/toolargetool.git'
                        developerConnection = 'scm:git:ssh://github.com/guardian/toolargetool.git'
                        url = 'https://github.com/guardian/toolargetool'

                    }
                    developers {
                        developer {
                            id = 'maxspencer'
                            name = 'Max Spencer'
                            email = 'max.spencer@guardian.co.uk'
                            url = 'https://github.com/maxspencer'
                            organization = 'The Guardian'
                            organizationUrl = 'https://theguardian.com'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'snapshot'
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
                credentials {
                    username = ossrhUsername ?: System.getenv("OSSRH_USERNAME")
                    password = ossrhPassword ?: System.getenv("OSSRH_PASSWORD")
                }
            }
            maven {
                name = 'staging'
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUsername ?: System.getenv("OSSRH_USERNAME")
                    password = ossrhPassword ?: System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}
