apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFile "proguard-rules.pro"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.24.0'
    androidTestImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group = 'com.gu.android'
                artifactId = 'toolargetool'
                version = '0.3.0'

                pom {
                    name = 'toolargetool'
                    description = 'A tool for debugging TransactionTooLargeException on Android'
                    url = 'https://github.com/guardian/toolargetool'
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    scm {
                        connection = 'https://github.com/guardian/toolargetool.git'
                        url = 'https://github.com/guardian/toolargetool'

                    }
                    developers {
                        developer {
                            id = 'maxspencer'
                            name = 'Max Spencer'
                        }
                    }
                }
            }
        }
    }
}

//apply from: 'bintray.gradle'
